apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'yaml.config.plugin'
    pluginVersion = '0.14.2'
    goCdVersion = '20.4.0'
    name = 'YAML Configuration Plugin'
    description = 'GoCD pipelines and environments configuration in YAML'
    vendorName = 'Tomasz Setkowski'
    vendorUrl = 'https://github.com/tomzo/gocd-yaml-config-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gocd-yaml-config-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.jar]
}

group = 'cd.go.plugin.config.yaml'
version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

ext {
    pluginApiVersion = '23.1.0'
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'org.apache.ant', name: 'ant', version: '1.10.13'
    implementation group: 'com.beust', name: 'jcommander', version: '1.82'
    implementation group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.15'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginApiVersion
    testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginApiVersion

    testImplementation platform('org.junit:junit-bom:5.9.3')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.4.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
    }
}

// create a fat jar with all dependencies
jar {
    archiveBaseName = project.name
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest {
        attributes 'Main-Class': 'cd.go.plugin.config.yaml.cli.YamlPluginCli'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "allow.class" // Noise from Ant
    exclude "META-INF/*.txt"
    exclude "META-INF/*.LIST"
    exclude "META-INF/versions/**"
}
